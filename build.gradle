// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = "1.3.72"
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.0.0"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

ext.versions = [
        "compileSDK"        : 29,
        "minSDK"            : 21,
        "targetSDK"         : 29,
        "dagger"            : "2.28.3",
        "recyclerview"      : "1.1.0",
        "constraintLayout"  : "1.1.3",
        "lifecycle"         : "2.2.0",
        "archTesting"       : "2.1.0",
        "fragment"          : "1.2.4",
        "coroutines"        : "1.3.6",
        "retrofit"          : "2.8.1",
        "coil"              : "0.11.0",
        "moshi"             : "1.9.2",
        "truth"             : "1.0.1"
]

ext.deps = [
        "androidx": [
                "recyclerview"      : "androidx.recyclerview:recyclerview:${versions.recyclerview}",
                "constraintLayout"  : "androidx.constraintlayout:constraintlayout:${versions.constraintLayout}",
                "livedata"          : "androidx.lifecycle:lifecycle-livedata-ktx:${versions.lifecycle}",
                "viewmodel"         : "androidx.lifecycle:lifecycle-viewmodel-ktx:${versions.lifecycle}",
                "fragment"          : "androidx.fragment:fragment-ktx:${versions.fragment}",
                "archtesting"       : "androidx.arch.core:core-testing:${versions.archTesting}",
                "fragmenttesting"   : "androidx.fragment:fragment-testing:${versions.fragment}",
        ],
        "dagger": [
                "core"      : "com.google.dagger:dagger:${versions.dagger}",
                "compiler"  : "com.google.dagger:dagger-compiler:${versions.dagger}"
        ],
        "coroutines": [
                "android"   : "org.jetbrains.kotlinx:kotlinx-coroutines-android:${versions.coroutines}",
                "test"      : "org.jetbrains.kotlinx:kotlinx-coroutines-test:${versions.coroutines}"
        ],
        "moshi"     : [
                "core"      : "com.squareup.moshi:moshi:${versions.moshi}",
                "kotlin"    : "com.squareup.moshi:moshi-kotlin-codegen:${versions.moshi}"
        ],
        "retrofit"  : [
                "core"      : "com.squareup.retrofit2:retrofit:${versions.retrofit}",
                "moshi"     : "com.squareup.retrofit2:converter-moshi:${versions.retrofit}"
        ],
        "coil"              : "io.coil-kt:coil:${versions.coil}",
        "truth"             : "com.google.truth:truth:${versions.truth}"

]

subprojects {
    plugins.withType(com.android.build.gradle.BasePlugin).whenPluginAdded {
        android {
            compileOptions {
                sourceCompatibility = JavaVersion.VERSION_1_8
                targetCompatibility = JavaVersion.VERSION_1_8
            }
        }
    }
}


task clean(type: Delete) {
    delete rootProject.buildDir
}